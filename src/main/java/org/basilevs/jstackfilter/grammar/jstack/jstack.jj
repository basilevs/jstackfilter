options
{
  STATIC = false;
  CHOICE_AMBIGUITY_CHECK = 10;
}

PARSER_BEGIN(JstackParser)
package org.basilevs.jstackfilter.grammar.jstack;
import org.basilevs.jstackfilter.Frame;

public final class JstackParser
{
  public static void main(String [] args) throws ParseException, TokenMgrError
  {
    JstackParser parser = new JstackParser(System.in);
    parser.JstackDump();
  }
}

PARSER_END(JstackParser)

TOKEN :
{
  < COMMENT : "(" ([ "a"-"z", " " ])+ ")" >
}

TOKEN :
{
  < QUOTE : "\"" (~[ "\"" ])* "\"" >
}

TOKEN :
{
  < COMMONSYMBOL : 
    ~[ " ", "\n", "\r", "{", "}", "_", "\t", "=", "\"", "#" ] >
}

TOKEN :
{
  < WORD :
    < COMMONSYMBOL >
    (
      "_"
    | < COMMONSYMBOL >
    )* >
}

TOKEN :
{
  < EOL :
    "\n"
  | "\r"
  | "\r\n" >
}

TOKEN :
{
  < FRAMEINDENT :
    "        "
  | "\t" >
}

TOKEN :
{
  < SPACE :
    " "
  | "  "
  | "   " >
}

void JStackHeader() :
{
}
{
  < TIME : [ "0"-"9" ] [ "0"-"9" ] [ "0"-"9" ] [ "0"-"9" ] "-" [ "0"-"9" ] [ "0"-"9" ] "-" [ "0"-"9" ] [ "0"-"9" ] " " [ "0"-"9" ] [ "0"-"9" ] ":" [ "0"-"9" ] [ "0"-"9" ] ":" [ "0"-"9" ] [ "0"-"9" ] > < EOL > 
  "Full thread dump " Line() < EOL > 
  < EOL > 
  "Threads class SMR info:" < EOL > 
  "_java_thread_list=" < WORD > " length=" < WORD > " elements={" < EOL >
  (
    Line() < EOL >
  )*
  "}" < EOL >
}

String Line() :
{
  StringBuilder image = new StringBuilder();
  Token t;
}
{
  t = < WORD >
  {
    image.append(t.image);
  }
  (
    LOOKAHEAD(2)
    t = < SPACE >
    {
      image.append(t.image);
    }
    t = < WORD >
    {
      image.append(t.image);
    }
  )*
  {
    return image.toString();
  }
}

Frame Frame() :
{
  String line = "";
}
{
  "at " 
  line = Line()
  {
    return new Frame(line);
  }
}

void Monitor() :
{
  String line = "";
}
{
  "- " Line()
}

void KeyValue() :
{
}
{
  < WORD > "="
  (
    < WORD >
  | < COMMONSYMBOL >
  )
}

//"VM Thread" os_prio=2 cpu=203.12ms elapsed=98699.54s tid=0x000002c22c0b2de0 nid=0x2c90 runnable
void NativeThread()  :
{
  
}
{
  < QUOTE > 
  ( LOOKAHEAD(2) 
    < SPACE >
    (
      LOOKAHEAD(2)
      KeyValue()
    | 
      < WORD >
    )
  )+
  < SPACE >
}

//"Worker-136" #2027 prio=5 os_prio=0 cpu=0.00ms elapsed=86.18s tid=0x000002c23074b590 nid=0x5884 in Object.wait()  [0x000000d6fb8fe000]
//   java.lang.Thread.State: TIMED_WAITING (on object monitor)
//	at java.lang.Object.wait(java.base@17/Native Method)
//	- waiting on <no object reference available>
//	at org.eclipse.core.internal.jobs.WorkerPool.sleep(WorkerPool.java:200)
//	- locked <0x0000000083286520> (a org.eclipse.core.internal.jobs.WorkerPool)
//	at org.eclipse.core.internal.jobs.WorkerPool.startJob(WorkerPool.java:242)
//	at org.eclipse.core.internal.jobs.Worker.run(Worker.java:58)
void JavaThread() :
{
}
{
  < QUOTE > < SPACE > "#"
  (
    < WORD >
  | < COMMONSYMBOL >
  )
  ( LOOKAHEAD(2) 
    < SPACE >
    (
      LOOKAHEAD(2)
      KeyValue()
    | 
      < WORD >
    )
  )+
  (< EOL >)? 
  < SPACE > "java.lang.Thread.State: " < WORD > (< SPACE > < COMMENT >)? < EOL >
  (
    < SPACE > "No compile task" < EOL >
  |
    (
      < FRAMEINDENT >
      (
        Monitor()
      | 
        Frame()
      )
      < EOL >
    )*
  )
}

void JstackDump() :
{
}
{
  JStackHeader() 
  < EOL >
  (
      (LOOKAHEAD(3)       JavaThread() | NativeThread() )
    < EOL >
  )*
}
